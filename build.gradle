buildscript {
    ext {
        kotlinVersion = '1.2.60'
        spekVersion = '2.0.0-rc.1'
        kluentVersion = '1.42'
        guavaVersion = '26.0-jre'
        exposedVersion = '0.10.4'
        ktorVersion = '0.9.5'
        kodeinVersion = '5.2.0'
        h2Version = '1.4.197'
        logbackVersion = '1.2.3'
        loggingVersion = '1.5.9'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.2.0'   // Required by Spek
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
    }
}

subprojects {

    apply plugin: 'kotlin'
    apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'idea'

    idea {
        module {
            downloadJavadoc = true // defaults to false
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
        jcenter()   // Required by Kluent
        maven { url "http://dl.bintray.com/jetbrains/spek" }   // Required by Spek 2 until a stable release
        maven { url "https://dl.bintray.com/spekframework/spek-dev" }
    }

    // Dependencies for Spek 2
    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testImplementation("org.spekframework.spek2:spek-dsl-jvm:$spekVersion") {
            exclude group: 'org.jetbrains.kotlin'
        }
        testRuntimeOnly("org.spekframework.spek2:spek-runner-junit5:$spekVersion") {
            exclude group: 'org.junit.platform'
            exclude group: 'org.jetbrains.kotlin'
        }

        // spek requires kotlin-reflect, can be omitted if already in the classpath
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    }

    dependencies {
        implementation "ch.qos.logback:logback-classic:$logbackVersion"
        implementation "io.github.microutils:kotlin-logging:$loggingVersion"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        testImplementation "org.amshove.kluent:kluent:$kluentVersion"
    }

    group = 'dfialho.tveebot'
    version = '0.1-SNAPSHOT'
    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'spek2'
        }
    }
}
