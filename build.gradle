buildscript {
    ext {
        kotlinVersion = '1.2.60'
        spekVersion = '1.1.5'
        kluentVersion = '1.40'
        springBootVersion = '2.0.4.RELEASE'
        guavaVersion = '26.0-jre'
        exposedVersion = '0.10.4'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.2.0'   // Required by Spek
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
    }
}

subprojects {

    apply plugin: 'kotlin'
    apply plugin: 'org.junit.platform.gradle.plugin'

    repositories {
        mavenCentral()
        jcenter()   // Required by Kluent
        maven { url "http://dl.bintray.com/jetbrains/spek" }   // Required by Spek 2 until a stable release
    }

    // Dependencies for Spek 2
    dependencies {
        compile 'io.github.microutils:kotlin-logging:1.5.9'
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testImplementation ("org.jetbrains.spek:spek-api:$spekVersion")  {
            exclude group: 'org.jetbrains.kotlin'
        }
        testRuntimeOnly ("org.jetbrains.spek:spek-junit-platform-engine:$spekVersion") {
            exclude group: 'org.junit.platform'
            exclude group: 'org.jetbrains.kotlin'
        }

        // Spek requires kotlin-reflect, can be omitted if already in the classpath
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
        testImplementation "org.amshove.kluent:kluent:$kluentVersion"
    }

    group = 'dfialho.tveebot'
    version = '0.1-SNAPSHOT'
    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'spek'
        }
    }
}
