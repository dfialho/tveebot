buildscript {
    ext {
        kotlinVersion = '1.2.60'
        spekVersion = '2.0.0-alpha.1'
        kluentVersion = '1.40'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0'   // Required by Spek
    }
}

allprojects {

    apply plugin: 'kotlin'
    apply plugin: 'org.junit.platform.gradle.plugin'

    repositories {
        mavenCentral()
        jcenter()   // Required by Kluent
        maven { url "https://dl.bintray.com/spekframework/spek-dev" }   // Required by Spek 2 until a stable release
    }

    // Dependencies for Spek 2
    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

        testImplementation ("org.spekframework.spek2:spek-dsl-jvm:$spekVersion")  {
            exclude group: 'org.jetbrains.kotlin'
        }
        testRuntimeOnly ("org.spekframework.spek2:spek-runner-junit5:$spekVersion") {
            exclude group: 'org.junit.platform'
            exclude group: 'org.jetbrains.kotlin'
        }

        // Spek requires kotlin-reflect, can be omitted if already in the classpath
        testRuntimeOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    }

    dependencies {
        testImplementation "org.amshove.kluent:kluent:$kluentVersion"
    }

    group = 'dfialho.tveebot'
    version = '0.1-SNAPSHOT'
    sourceCompatibility = 1.8

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    test {
        useJUnitPlatform {
            includeEngines 'spek2'
        }
    }
}
